" From https://klamp.works/2016/01/01/vim-splash.html
fun! Start()

  "Create a new unnamed buffer to display our splash screen inside of.
  enew

  " Set some options for this buffer to make sure that does not act like a
  " normal winodw.
  setlocal
        \ bufhidden=wipe
        \ buftype=nofile
        \ nobuflisted
        \ nocursorcolumn
        \ nocursorline
        \ nolist
        \ nonumber
        \ noswapfile
        \ norelativenumber
        \ textwidth=0

  " Our message goes here. Mine is simple.
  "   call append('$', "
  "   \")
  syn match deltaFg '\.'
  syn match deltaFg '[\.\`]:*[\.\'\`]'
  syn match deltaBg '\*'
  syn keyword deltaTxt EX ASTRIS SCIENTIA
  hi deltaBg term=reverse ctermfg=8 ctermbg=3 guifg=#838085 guibg=#dc8a0e
  hi deltaFg ctermfg=0 ctermbg=7 guifg=#2e2a31 guibg=#bcbabe
  hi deltaTxt ctermfg=6 guifg=#149bda
  hi clear ExtraWhitespace
  IndentLinesDisable
  r ~/.vim/delta.txt
  let padding_columns = (&columns - 31) / 2
  let padding = repeat(' ', padding_columns)
  %s/^/\=padding/
  echo 'Live Long, and Prosper'

  " When we are done writing out message set the buffer to readonly.
  setlocal
        \ nomodifiable
        \ nomodified

  setlocal synmaxcol=0

  " Just like with the default start page, when we switch to insert mode
  " a new buffer should be opened which we can then later save.
  nnoremap <buffer><silent> e :enew<CR>
  nnoremap <buffer><silent> i :enew <bar> startinsert<CR>
  nnoremap <buffer><silent> o :enew <bar> startinsert<CR>
endfun

fun! Clear()
  augroup greeting
    autocmd!
  augroup END
  hi clear deltaBg
  hi clear deltaFg
  hi clear deltaTxt
  hi ExtraWhitespace ctermbg=9 guibg=#FF0000

  " Make it obvious where 80 characters is
  " set textwidth=80
  " set colorcolumn=+1
endfun

" http://learnvimscriptthehardway.stevelosh.com/chapters/12.html
" Autocommands are a way of setting handlers for certain events.
" `VimEnter` is the event we want to handle. http://vimdoc.sourceforge.net/htmldoc/autocmd.html#VimEnter
" The cleene star (`*`) is a pattern to indicate which filenames this Autocommand will apply too. In this case, star means all files.
" We will call the `Start` function to handle this event.

" http://vimdoc.sourceforge.net/htmldoc/eval.html#argc%28%29
" The number of files in the argument list of the current window.
" If there are 0 then that means this is a new session and we want to display
" our custom splash screen.
if argc() == 0
  augroup greeting
    autocmd VimEnter * call Start()
    autocmd BufRead * call Clear()
  augroup END
endif
